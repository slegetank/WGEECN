\chapter{协作命令}
\label{chapter:03-Cooperating-Commands}

\guide{
\begin{itemize}
  \item 症状
  \item 解药
  \item 归纳出更一般的解决方法
\end{itemize}
}

本章将讲述不同命令协同工作，以完成在一个命令里保存信息而在另一个命令里获取它。共享信息的最简单的方式是创建一个变量并把值存进去。本章中我们当然也会这么做。例如，我们会把当前buffer中的位置信息存储起来然后在其他命令中使用它。但是我们也会学到一些更复杂的保存状态的方式，特别是标记（markers）和符号属性（symbol properties）。我们将会把这些跟buffer和窗口的信息组合起来写出一套允许你“回滚”翻页动作的函数。

\section{症状}
\label{section:03-The-Symptom}

你正在编写一些复杂的Lisp代码。你非常专注，在头脑中努力的理清数据之间的微妙的联系然后将它们编写到屏幕上。你正在处理一个非常精妙的部分而这时你发现了左面的一个拼写错误。你希望按下 \verb|C-b| \verb|C-b| \verb|C-b| 来回到那里对其修正，但是---灾难发生了---你按下了 \verb|C-v| \verb|C-v| \verb|C-v| ，向下翻了三屏，最终停在了离你的代码几光年以外的地方。你试图找到在这个失误发生之前光标在哪里，以及为什么在那里，以及你正在那里做什么，这无疑打乱了你头脑中的思考。当你翻页，或者搜索，或者查找撤销列表来回到那里时，你已经忘了最初想要修正的那个拼写错误，最终这变成了一个bug，可能会花费你几个小时来找到它。

Emacs在这个例子中没有起到帮助作用。它使你如此容易的在你的文档中迷失而找到回去的路却很困难。虽然Emacs有一个可扩展的撤销工具，但这只能撤销修改。你不能撤销如此简单的浏览行为。

\section{解药}
\label{section:03-A-Cure}

假设我们可以这样修改 \verb|C-v| （ \mintinline{elisp}{scroll-up} 命令\footnote{虽然在第\ref{chapter:02-Simple-New-Commands}章，我们使用了 \mintinline{elisp}{defalias} 将 \mintinline{elisp}{scroll-ahead} 和 \mintinline{elisp}{scroll-behind} 替代了 \mintinline{elisp}{scroll-up} 和 \mintinline{elisp}{scroll-down} ，本章我们依然使用它们之前的名称。}），当我们按下它之后，Emacs认为，“可能用户是误按的 \verb|C-v| ，所以我将记录一些‘撤销’的信息以备未来需要”。然后我们可以编写另一个函数， \mintinline{elisp}{unscroll} ，它可以回滚最后一次的滚动。这样只要记住 \mintinline{elisp}{unscroll} 的按键绑定就能避免再次发生这种迷失。

实际上这并没完全解决问题。如果你一次按下了多个 \verb|C-v| ，调用一次 \mintinline{elisp}{unscroll} 应该将它们都撤销，而不是只撤销最后一次。这表示只有序列中的第一个 \verb|C-v| 才需要记住它的起始位置。我们怎样来检测这种情况的发生呢？在我们的 \verb|C-v| 代码里记录开始位置之前，我们需要知道（a）下一个命令是否还是 \mintinline{elisp}{scroll-up} ，或者（b）前一个命令是否不是 \mintinline{elisp}{scroll-up} 。显然（a）是不可能的：我们不能预见未来。幸运的是（b）很简单：Emacs有一个称为 \mintinline{elisp}{last-command} 的变量专门记录这一信息。这个变量是我们将使用的在不同命令间传递信息的第一个方法。

现在唯一剩下的问题是：我们如何把这个额外的代码关联到 \mintinline{elisp}{scroll-up} ？修饰特性能很好的解决这一问题。前面讲过一段修饰代码可以在被修饰方法之前或之后执行。在本例中我们需要前置修饰，因为只有在执行 \mintinline{elisp}{scroll-up} 之前我们才能知道开始的位置在哪里。

\subsection{声明变量}
\label{section:03-Declaring-Variables}

我们将以建立一个保存“撤销”信息的全局变量 \mintinline{elisp}{unscorll-to} 开始，它保存了 \mintinline{elisp}{unscroll} 应该将光标移动到的位置。我们将使用 \mintinline{elisp}{defvar} 来声明变量。

\begin{minted}{elisp}
(defvar unscroll-to nil
  "Text position for next call to 'unscroll'.")
\end{minted}

全局变量不需要声明。但是使用 \mintinline{elisp}{defvar} 声明变量有一些优势：

\begin{itemize}
  \item 使用 \mintinline{elisp}{defvar} 能够关联一个文档字符串给变量，就像 \mintinline{elisp}{defun} 那样。
  \item 可以给变量赋一个默认值。本例中， \mintinline{elisp}{unscroll-to} 的默认值是 \mintinline{elisp}{nil} 。
通过 \mintinline{elisp}{defvar} 给变量赋默认值与使用 \mintinline{elisp}{setq} 不一样。使用 \mintinline{elisp}{setq} 将会强制给变量赋默认值，而 \mintinline{elisp}{defvar} 只会在变量没有任何值时才会赋给默认值。
为什么这很重要呢？假设你的 \verb|.emacs| 文件有这么一行：
\begin{minted}{elisp}
(setq mail-signature t)
\end{minted}
这表示当你使用Emacs发送email时，你希望在最后添加你自己的签名文件。当你启动Emacs时， \mintinline{elisp}{mail-signature} 被设置为了 \mintinline{elisp}{t} ，但是定义邮件发送代码的Lisp文件，sendmail，还没有被载入。它只有当你第一次调用mail命令时才会加载。当你调用mail时，Emacs将执行sendmail Lisp文件中的代码：
\begin{minted}{elisp}
(defvar mail-signature nil ...)
\end{minted}
这表示 \mintinline{elisp}{nil} 是 \mintinline{elisp}{mail-signature} 的默认初始值。但是你已经给 \mintinline{elisp}{mail-signature} 赋过值了，而你不希望载入sendmail时把你的设置给覆盖了。另一方面，如果你在 \verb|.emacs| 中并没有给 \mintinline{elisp}{mail-signature} 赋过任何值，你还是希望这个值能够生效。
  \item 使用 \mintinline{elisp}{defvar} 声明的变量可以通过多个标签相关（tag-related）的命令找到。在工程中通过标签找到变量和函数定义是一种快捷的方式。Emacs的标签工具，例如 \mintinline{elisp}{find-tag} 函数，可以找到任何通过def...函数( \mintinline{elisp}{defun} , \mintinline{elisp}{defalias} , \mintinline{elisp}{defmacro} , \mintinline{elisp}{defvar} , \mintinline{elisp}{defsubst} , \mintinline{elisp}{defconst} , \mintinline{elisp}{defadvice} )定义的东西。
  \item 当你编译字节码时（见第\ref{chapter:05-Lisp-Files}章），编译器如果发现变量未使用 \mintinline{elisp}{defvar} 声明将会产生一个警告。如果你的所有变量都进行了声明，那么你可以使用这些警告来找出哪些变量名写错了。
\end{itemize}

\subsection{保存和取出point}
\label{section:03-Saving-and-Restoring-Point}

让我们定义一个变量来存储 \mintinline{elisp}{scroll-up} 队列的最初位置，即最初光标在文本中的位置。光标在文本中的位置被称为point，其值就是从buffer开始位置计算（从1开始）有多少个字符。point的值可以由函数point得到。

\begin{minted}{elisp}
(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (eq last-command 'scroll-up))
      (setq unscroll-to (point))))
\end{minted}

这个修饰是这么工作的：

\begin{enumerate}
  \item 函数 \mintinline{elisp}{eq} 告诉我们它的两个参数是否相等。本例中，参数是 \mintinline{elisp}{last-command} 变量的值，以及符号 \mintinline{elisp}{scroll-up} 。 \mintinline{elisp}{last-command} 的值是最后一次用户触发的命令的符号（在本章后面的部分\nameref{section:03-Using-this-command}中也能看到）。
  \item \mintinline{elisp}{eq} 的返回值被传递给 \mintinline{elisp}{not} ，这会对它的参数的布尔值取反。如果 \mintinline{elisp}{nil} 传给 \mintinline{elisp}{not} ，那么返回 \mintinline{elisp}{t} 。如果其他值传给 \mintinline{elisp}{not} ，则返回 \mintinline{elisp}{nil} 。\footnote{如果你认为 \mintinline{elisp}{not} 的行为看起来跟 \mintinline{elisp}{null} 很像，你是对的---它们就是同一个函数。它们其中一个就是另一个的别名。你使用哪个只是一个可读性方面的问题。当要检测一个列表是否为空列表时使用 \mintinline{elisp}{null} 。当要对一个值取反的时候使用 \mintinline{elisp}{not} 。}
  \item 如果 \mintinline{elisp}{not} 的返回值是 \mintinline{elisp}{t} ，即 \mintinline{elisp}{last-command} 的值并不是 \mintinline{elisp}{scroll-up} ，那么变量 \mintinline{elisp}{unscroll-to} 的的值将被设置为当前的point值。
\end{enumerate}

现在定义 \mintinline{elisp}{unscroll} 就很简单了：

\begin{minted}{elisp}
(defun unscroll ()
  "Jump to location specified by 'unscroll-to'."
  (interactive)
  (goto-char unscroll-to))
\end{minted}

函数 \mintinline{elisp}{goto-char} 将光标移动到指定的位置。

\subsection{窗口内容}
\label{section:03-Window-Appearance}

对于这个解决方案有一些不完美的地方。在一次 \mintinline{elisp}{unscroll} 之后，光标确实返回到了正确的地方，但这时屏幕却看起来和按下 \verb|C-v| 之前不一样了。例如，我在按下 \verb|C-v| \verb|C-v| \verb|C-v| 之前可能正在屏幕的底部编辑一行代码。在我调用 \mintinline{elisp}{unscroll} 之后，虽然光标确实回到了之前的位置，但是那一行可能显示到了窗口的中间。

既然我们的目的是最小化意料之外的滚动所造成的破坏，那么我们不只希望仅仅恢复光标的位置，我们还希望之前编辑的行的位置也恢复到原处。

因此只保存point的值就不够了。我们还必须保存一个值来表示当前窗口中显示什么。Emacs提供了几个函数来描述窗口中显示什么，例如 \mintinline{elisp}{window-edges} ， \mintinline{elisp}{window-height} ， \mintinline{elisp}{current-window-configuration} 。目前我们只需要使用 \mintinline{elisp}{window-start} ，它表示对于给定的窗口，显示的第一个（窗口左上角）字符在buffer中的位置。这样我们只需要在命令间传递更多一点信息就可以了。

更新我们的例子很简单。首相我们要将变量 \mintinline{elisp}{unscroll-to} 的声明替换为两个新的变量：一个用于保存point的值，另一个用于保存窗口中第一个字符的位置。

\begin{minted}{elisp}
(defvar unscroll-point nil
  "Cursor position for next call to 'unscroll'.")
  
(defvar unscroll-window-start nil
  "Window start for next call to 'unscroll'.")
\end{minted}

然后我们要修改 \mintinline{elisp}{scroll-up} 的修饰以及 \mintinline{elisp}{unscroll} 来使用这两个值。

\begin{minted}{elisp}
(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for ‘unscroll'."
  (if (not (eq last-command 'scroll-up))
      (progn
        (setq unscroll-point (point))
        (setq unscroll-window-start (window-start)))))

(defun unscroll ()
  "Revert to 'unscroll-point' and 'unscroll-window-start'."
  (interactive)
  (goto-char unscroll-point)
  (set-window-start nil unscroll-window-start))
\end{minted}

修饰的名称仍然是 \mintinline{elisp}{remember-for-unscroll} ，这会替换之前同名的修饰。

函数 \mintinline{elisp}{set-window-start} 和 \mintinline{elisp}{goto-char} 移动光标位置的方式类似，它会设置窗口开始的位置。不一样的是， \mintinline{elisp}{set-window-start} 有两个参数。第一个参数表明操作的是哪个窗口。如果为 \mintinline{elisp}{nil} ，则默认使用当前选中的窗口。（传递给 \mintinline{elisp}{set-window-start} 的窗口对象可以通过类似 \mintinline{elisp}{get-buffer-window} 以及 \mintinline{elisp}{previous-window} 的函数得到。）

对于回滚我们可能还希望保持另一个信息，即窗口的 \mintinline{elisp}{hscroll} ，它保存着窗口横向翻滚的列数，默认为0 。我们可以添加另一个变量来保存：

\begin{minted}{elisp}
(defvar unscroll-hscroll nil
  "Hscroll for next call to 'unscroll'.")
\end{minted}

然后我们再次更新 \mintinline{elisp}{unscroll} 和 \mintinline{elisp}{scroll-up} 修饰来调用 \mintinline{elisp}{window-hscroll} （获取窗口的 \mintinline{elisp}{hscroll} 值）以及 \mintinline{elisp}{set-window-hscroll} （设置）：

\begin{minted}{elisp}
(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (eq last-command 'scroll-up))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defun unscroll ()
  "Revert to 'unscroll-point' and 'unscroll-window-start'."
  (interactive)
  (goto-char unscroll-point)
  (set-window-start nil unscroll-window-start)
  (set-window-hscroll nil unscroll-hscroll))
\end{minted}

注意在这个 \mintinline{elisp}{scroll-up} 修饰的版本中， \mintinline{elisp}{progn} 的使用：

\begin{minted}{elisp}
(progn
  (setq ...)
  (setq ...))
\end{minted}

被合并成了一个 \mintinline{elisp}{setq} ，里面包含了多个“变量-值”对。这是一种简化写法， \mintinline{elisp}{setq} 可以包含任意数量的变量。

\subsection{错误检查}
\label{section:03-Detecting-Errors}

假如用户在调用任何 \mintinline{elisp}{scroll-up} 之前调用了 \mintinline{elisp}{unscroll} 会发生什么呢？变量 \mintinline{elisp}{unscroll-point} ， \mintinline{elisp}{unscroll-window-start} ，以及 \mintinline{elisp}{unscroll-scroll} 将会包含他们的默认值，也就是 \mintinline{elisp}{nil} 。这个值在传递给函数 \mintinline{elisp}{goto-char} ， \mintinline{elisp}{set-window-start} 以及 \mintinline{elisp}{set-window-scroll} 时是不合适的。当 \mintinline{elisp}{goto-char} 被调用时， \mintinline{elisp}{unscroll} 的触发将会返回如下错误：“Wrong type argument: integer-or-maker-p, nil”。这表示一个函数需要接收一个数字或者标记（满足断言 \mintinline{elisp}{integer-or-marker-p} ），而收到的却是 \mintinline{elisp}{nil} 。（标记在本章前面的部分介绍过了。）

为避免用户被这些神秘的错误信息所折磨，在调用 \mintinline{elisp}{goto-char} 之前进行一个简单的检查并且生成一个更可读的错误信息是一个好主意：

\begin{minted}{elisp}
(if (not unscroll-point) ; 如果unscroll-point的值为nil
    (error "Cannot unscroll yet"))
\end{minted}

当错误发生时， \mintinline{elisp}{unscroll} 将会被终止并且提示“Cannot unscroll yet”。

\section{归纳出更一般的解决方法}
\label{section:03-Generalizing-the-Solution}

当我们想要按 \verb|C-b| 时按到 \verb|C-v| 是很常见的一种情况。这也就是我们设计 \mintinline{elisp}{unscroll} 函数的原因。现在让我们来研究同样容易发生的想按下 \verb|M-b| （ \mintinline{elisp}{backward-word} ）却按下了 \verb|M-v| （ \mintinline{elisp}{scroll-down} ）。这是同样的问题，但也有点不一样。如果 \mintinline{elisp}{unscroll} 能够回撤任何方向的滚动就好了。

最直接的方法是像修饰 \mintinline{elisp}{scroll-down} 那样修饰 \mintinline{elisp}{scroll-up} ：

\begin{minted}{elisp}
(defadvice scroll-down (before remember-for-unscroll
                               activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (eq last-command 'scroll-down))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))
\end{minted}

（注意这两个函数， \mintinline{elisp}{scroll-up} 和 \mintinline{elisp}{scroll-down} ，它们的修饰的名称，也就是 \mintinline{elisp}{remember-for-unscroll} ，可以一样，而且不会冲突。）

现在我们必须决定当错误的 \verb|C-v| 和错误的 \verb|M-v| 同时发生时 \mintinline{elisp}{unscroll} 如何运作。换句话说，假设你错误的按下了 \verb|C-v| \verb|C-v| \verb|M-v| 。它是应该恢复到 \verb|M-v| 之前的位置呢，还是应该恢复到最初的 \verb|C-v| 之前？

我选择后者。但是这意味着对于 \mintinline{elisp}{scroll-up} （以及 \mintinline{elisp}{scroll-down} ）的修饰，我们需要同时检测 \mintinline{elisp}{scroll-up} 和 \mintinline{elisp}{scroll-down} 。

\begin{minted}{elisp}
(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defadvice scroll-down (before remember-for-unscroll
                               activate compile)
  "Remember where we started from, for 'unscroll'.'"
        (if (not (or (eq last-command 'scroll-up)
                (eq last-command 'scroll-down)))
        (setq unscroll-point (point)
                unscroll-window-start (window-start)
                unscroll-hscroll (window-hscroll))))
\end{minted}

让我们花一点时间来确保你理解表达式

\begin{minted}{elisp}
(if (not (or (eq last-command 'scroll-up)
             (eq last-command 'scorll-down)))
    (setq ...))
\end{minted}

阅读这段表达式最好的方法是一级一级的向里阅读。从这里开始

\begin{minted}{elisp}
(if (not ...)
    (setq ...))
\end{minted}

“如果为假，则设置一些变量。”下面更进一步：

\begin{minted}{elisp}
(if (not (or ...))
    (setq ...))
\end{minted}

“如果所有条件都为假，则设置一些变量。”最后，

\begin{minted}{elisp}
(if (not (or (eq last-command 'scroll-up)
             (eq last-command 'scorll-down)))
    (setq ...))
\end{minted}

表示，“如果 \mintinline{elisp}{last-command} 不是 \mintinline{elisp}{scroll-up} 并且 \mintinline{elisp}{last-command} 不是 \mintinline{elisp}{scroll-down} ，那么设置一些变量。”

假设之后你希望更多的命令也按照这种方式来修饰；例如 \mintinline{elisp}{scroll-left} 和 \mintinline{elisp}{scroll-right} ：

\begin{minted}{elisp}
(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for 'unscroll'. "
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)
               (eq last-command 'scroll-left) ;new
               (eq last-command 'scroll-right))) ;new
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defadvice scroll-down (before remember-for-unscroll
                               activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)
               (eq last-command 'scroll-left) ;new
               (eq last-command 'scroll-right))) ;new
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defadvice scroll-left (before remember-for-unscroll
                               activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)
               (eq last-command 'scroll-left)
               (eq last-command 'scroll-right)))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defadvice scroll-right (before remember-for-unscroll
                                activate compile)
  "Remember where we started from, for 'unscroll'."
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)
               (eq last-command 'scroll-left)
               (eq last-command 'scroll-right)))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))
\end{minted}

这样写不仅繁琐且容易出错，而且对于每个我们需要回撤的新命令，每个之前写过的回撤命令都需要加入对于新的 \mintinline{elisp}{last-command} 的检测。

\subsection{使用this-command}
\label{section:03-Using-this-command}

有两种方法可以改善这种情况。第一种，既然每个修饰都差不多，我们可以把它们提取一下：

\begin{minted}{elisp}
(defun unscroll-maybe-remember ()
  (if (not (or (eq last-command 'scroll-up)
               (eq last-command 'scroll-down)
               (eq last-command 'scroll-left)
               (eq last-command 'scroll-right)))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))

(defadvice scroll-up (before remember-for-unscroll
                             activate compile)
  "Remember where we started from, for 'unscroll'."
  (unscroll-maybe-remember))

(defadvice scroll-down (before remember-for-unscroll
                               activate compile)
  "'Remember where we started from, for 'unscroll'."
  (unscroll-maybe-remember))

(defadvice scroll-left (before remember-for-unscroll
                               activate compile)
  "Remember where we started from, for 'unscroll'."
  (unscroll-maybe-remember))

(defadvice scroll-right (before remember-for-unscroll
                                activate compile)
  "Remember where we started from, for 'unscroll'."
  (unscroll-maybe-remember))
\end{minted}

第二种，不去检测 \mintinline{elisp}{n} 种可能的 \mintinline{elisp}{last-command} 值，我们可以使用一个单独的变量来保存每种情况的 \mintinline{elisp}{last-command} 值。

当前用户触发的命令的名称会保存在变量 \mintinline{elisp}{this-command} 中。实际上， \mintinline{elisp}{last-command} 的值是这样得到的：当Emacs执行一个命令时， \mintinline{elisp}{this-command} 保存着命令的名字；当执行完成时，Emacs会将 \mintinline{elisp}{this-command} 的值赋给 \mintinline{elisp}{last-command} 。

当命令执行时，它会改变 \mintinline{elisp}{this-command} 的值。当下个命令执行时，这个值会保存在 \mintinline{elisp}{last-command} 中。

让我们选择一个符号来代表所有可回撤的命令：例如， \mintinline{elisp}{unscrollable} 。现在我们可以修改一下 \mintinline{elisp}{unscroll-maybe-remeber} ：

\begin{minted}{elisp}
(defun unscroll-maybe-remember ()
  (setq this-command 'unscrollable)
  (if (not (eq last-command 'unscrollable))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))
\end{minted}

调用这个函数的命令会将 \mintinline{elisp}{this-command} 设置为 \mintinline{elisp}{unscrollable} 。现在我们只需要检测一个变量而不必检测四种不同情况的 \mintinline{elisp}{last-command} （也许还会更多）了。

\subsection{符号属性}
\label{section:03-Symbol-Properties}

我们改进版的 \mintinline{elisp}{unscroll-maybe-remeber} 工作的非常好，但是（你可能已经预料到了）我们还是可以做一些改进。首先就是变量 \mintinline{elisp}{this-command} 和 \mintinline{elisp}{last-command} 并不是只有我们自己使用。它们对于Emacs Lisp解释器很重要，而Emacs的其他功能也依赖于这两个值。而我们知道，有一些使用了这些滚动函数的Emacs特性并没有修改 \mintinline{elisp}{this-commandh} 和 \mintinline{elisp}{last-command} 。而且，我们更想要一个专有的值来标识所有可回滚的命令。

这里我们引入好用的符号属性（symbol properties）。Emacs Lisp符号不只用来保存函数定义，它还有一组关联的属性列表。属性列表是一组键值映射。每个名字是一个Lisp符号，每个值可以是任意Lisp表达式。

属性使用 \mintinline{elisp}{put} 函数来保存，使用 \mintinline{elisp}{get} 函数来读取。因此，如果我们将17保存在符号 \mintinline{elisp}{a-symbol} 的 \mintinline{elisp}{some-property} 的属性中：

\begin{minted}{elisp}
(put 'a-symbol 'some-property 17)
\end{minted}

那么

\begin{minted}{elisp}
(get 'a-symbol 'some-property)
\end{minted}

将返回17 。如果我们从一个符号读取一个并不存在的属性，则返回 \mintinline{elisp}{nil} 。

我们可以将 \mintinline{elisp}{unscrollable} 作为一个属性，而不是作为一个储存 \mintinline{elisp}{this-command} 和 \mintinline{elisp}{last-command} 的值的变量。我们可以将支持返回的命令的 \mintinline{elisp}{unscrollable} 属性设为 \mintinline{elisp}{t} ：

\begin{minted}{elisp}
(put 'scroll-up 'unscrollable t)
(put 'scroll-down 'unscrollable t)
(put 'scroll-left 'unscrollable t)
(put 'scroll-right 'unscrollable t)
\end{minted}

这只需要在调用 \mintinline{elisp}{unscroll-maybe-remember} 之前执行一次就行了。

现在如果 \mintinline{elisp}{x} 是 \mintinline{elisp}{scroll-up} ， \mintinline{elisp}{scroll-down} ， \mintinline{elisp}{scroll-left} ， \mintinline{elisp}{scroll-right} 之中的一个的话则 \mintinline{elisp}{(get x unscrollable)} 会返回 \mintinline{elisp}{t} 。对于其他的符号，因为 \mintinline{elisp}{unscrollable} 属性默认未定义，所以结果为 \mintinline{elisp}{nil} 。

现在我们可以将 \mintinline{elisp}{unscroll-maybe-remember} 中的

\begin{minted}{elisp}
(if (not (eq last-command 'unscrollable)) ...)
\end{minted}

修改为

\begin{minted}{elisp}
(if (not (get last-command 'unscrollable)) ...)
\end{minted}

而且我们还可以停止将 \mintinline{elisp}{unscrollable} 赋值给 \mintinline{elisp}{this-command} ：

\begin{minted}{elisp}
(defun unscroll-maybe-remember ()
  (if (not (get last-command 'unscrollable))
      (setq unscroll-point (point)
            unscroll-window-start (window-start)
            unscroll-hscroll (window-hscroll))))
\end{minted}

\subsection{标记}
\label{section:03-Markers}

我们能否将这段代码改的更好呢？假设你不小心按下了几次 \mintinline{elisp}{scroll-down} 然后你想 \mintinline{elisp}{unscroll} 。但是在你这么做之前，你发现了一些你想要修改的代码，然后你进行了修改。然后你再 \mintinline{elisp}{unscroll} 。这时屏幕并没有被正确的恢复！

这是因为编辑buffer中前面的文字将会改变所有后面的文字的位置。添加或者删除 \mintinline{elisp}{n} 个字符将会对所有后续的字符位置添加或减少 \mintinline{elisp}{n} 。因此 \mintinline{elisp}{unscroll-point} 和 \mintinline{elisp}{unscroll-window-start} 所保存的值都会被影响 \mintinline{elisp}{n} （如果 \mintinline{elisp}{n} 为0，那么你很幸运）。

使用标记（marker）而不是 \mintinline{elisp}{unscroll-point} 和 \mintinline{elisp}{unscroll-window-start} 的绝对位置将会是一个很好的选择。标记是一种就像数字一样用来保存buffer位置的特殊对象。但是如果由于插入或者删除造成了buffer位置的更改，那么标记也会跟着更改。

既然我们要将 \mintinline{elisp}{unscroll-point} 和 \mintinline{elisp}{unscroll-window-start} 修改为标记，我们就不需要将他们初始化为 \mintinline{elisp}{nil} 了。我们可以使用 \mintinline{elisp}{make-marker} 来将它们初始化为空的标记对象：

\begin{minted}{elisp}
(defvar unscrollfpoint (make-marker)
  "Cursor position for next call to 'unscroll'.")
  
(defvar unscroll-window-start (make-marker)
  "Window start for next call to ''unscroll'.")
\end{minted}

函数 \mintinline{elisp}{set-marker} 被用来设置标记的位置。

\begin{minted}{elisp}
(defun unscroll-maybe-remember ()
  (if (not (get last-command 'unscrollable))
      (progn
        (set-marker unscroll-point (point))
        (set-marker unscroll-window-start (window-start))
        (setq unscroll-hscroll (window-hscroll)))))
\end{minted}

\mintinline{elisp}{progn} 又回来了，因为 \mintinline{elisp}{setq} 被拆分成了几个不同的函数调用。我们不对 \mintinline{elisp}{unscroll-hscroll} 使用标记，因为它的值并不是buffer位置。

我们并不需要重写 \mintinline{elisp}{unscroll} ，因为 \mintinline{elisp}{goto-char} 和 \mintinline{elisp}{set-window-start} 的参数不管是标记还是数字都会很好的工作。所以前面的定义（为了方便这里在贴一次）还是能够工作：

\begin{minted}{elisp}
(defun unscroll ()
  "Revert to 'unscroll-point' and 'unscroll-window-start'."
  (interactive)
  (goto-char unscroll-point)
  (set-window-start nil unscroll-window-start)
  (set-window-hscroll nil unscroll-hscroll))
\end{minted}

\subsection{附录：关于效率}
\label{section:03-Addendum-Efficiency-Consideration}

当我们定义 \mintinline{elisp}{unscroll-point} 和 \mintinline{elisp}{unscroll-marker} 时，我们创建了空的符号对象并且在每次调用 \mintinline{elisp}{unscroll-remember} 时复用它们，而不是每次都创建新的并且扔掉旧的。这是一种优化。这并不仅仅是说我们应该尽可能的避免这种对象创建的消耗，更多的是因为标记要比其他变量的消耗更大。每次buffer做出修改的时候标记都会跟着修改。弃用的标记最终会被垃圾回收器回收掉，但是直到那时它都会降低编辑buffer的速度。

通常，如果你想要弃用一个标记对象 \mintinline{elisp}{m} （即你不需要再使用它的值了），这么做将会是很好的选择：

\begin{minted}{elisp}
(set-marker m nil)
\end{minted}
